# Maintainer: Tarmo Heiskanen <turskii@gmail.com>
# Contributor: mar77i <mar77i at mar77i dot ch>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.8.4.r7.g2f6e597
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st#commit=2f6e597ed871cff91c627850d03152cae5f45779'
  'config.h'
  'st-appsync-20200618-b27a383-fixed.diff'
  'st-externalpipe-0.8.4.diff'
  'st-focus-20200731-43a395a-fixed.diff'
  'st-hidecursor-0.8.3-fixed.diff'
  'st-xft-fix.diff'
  'st.desktop')
sha1sums=('SKIP'
          '9d4856fef01485148167de9d7d732931c96385f3'
          '83eb8cb7baecfac76ec0636daa599764be6e6b10'
          'aee72b8ea01e9d970769e1ef23a8b3d5efcf1dac'
          '7f80a82aec5f6b1451f9165970317338cf062684'
          '9171a8ac8199c53b3056be7b244f9db17053a6a1'
          'ec588beba09d0ee1cd3e27072e81bed0cee2416a'
          'bc7d6fff88c401a5143000f43f3c5fab1d48e974')
provides=('st')
conflicts=('st')

pkgver() {
  cd "${srcdir}/st"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/st"
  git apply --reject --ignore-space-change $srcdir/st-appsync-20200618-b27a383-fixed.diff
  git apply --reject --ignore-space-change $srcdir/st-externalpipe-0.8.4.diff
  git apply --reject --ignore-space-change $srcdir/st-focus-20200731-43a395a-fixed.diff
  git apply --reject --ignore-space-change $srcdir/st-hidecursor-0.8.3-fixed.diff
  git apply --reject --ignore-space-change $srcdir/st-xft-fix.diff
  cp $srcdir/config.h config.h
  cp $srcdir/st.desktop st.desktop
}

build() {
  cd "${srcdir}/st"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/st"
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="/dev/null" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 st.desktop "${pkgdir}/usr/share/applications/st.desktop"
}
